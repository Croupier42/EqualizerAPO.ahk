;Создан croupier https://web.telegram.org/#@Croupier42
;Основан на базе https://github.com/Brad331/APOpreamp.ahk
;Версия скрипта 0.6
;Требуется AutoHotkey 1.1
;Зачем я это сделал? Потому что я ненавижу системную регулировку винды. Ну и для смены профилей APO по горячей клавише.
;Перед запуском скрипта обязательно применить в EqualizerAPO либо не включать звук, а то можно получить максимальной громкостью по ебалу!
;Если бы я мог измерить звуковое давление системы... я бы мог сделать значения громкости привязанными к реалу... правда оно было бы привязано только к одной системе...
;В два раза мощнее для акустики - 6дБ ; в два раза больше напряжение для акустики - 3дБ
;В два раза громче для человека - 10дБ; максимально удобный шаг изменения громкости для меня - 5дБ

;переменные--------------------------------------------------------------------------------------------------------------------------------------------------------
global GainFile := "C:\Program Files\EqualizerAPO\config\ahk\Gain.txt"				;Изменяемый файл усиления, его нужно применить в EqualizerAPO.
global Gain := -50																	;Текущее усиление, а также значение при запуске скрипта
global GainMax := 10																;Максимальный уровень усиления, не рекомендую использовать выше 0 без индикатора громкости или лимитера
global GainMin := -60																;Минимальный уровень усиления

global ProfileFile := "C:\Program Files\EqualizerAPO\config\ahk\Profile.txt"		;Изменяемый файл профиля, его нужно применить в EqualizerAPO.
global ProfileA := "C:\Program Files\EqualizerAPO\config\NOEMA\NOEMA Filters.txt"										;Расположение профиля А
global ProfileB := "C:\Program Files\EqualizerAPO\config\Headphones\Zero_2\7Hz-Salnotes x Crinacle Zero_2 Filters.txt"	;Расположение профиля B
global Profile := ProfileA															;Текущий профиль
global Label := "NOEMA"
;Можно насозавать кучу профилей и переключаться между ними как хочешь... Добавляй кучу if, или привязывай профили к конкретным клавишам...

;OSD--------------------------------------------------------------------------------------------------------------------------------------------------------
Gui +LastFound +AlwaysOnTop -Caption +ToolWindow									;Окно OSD
Gui, Color, 000000																	;Цвет фона
Gui, Font, s24,																		;Размер шрифта
Gui, Margin, 4, 0																	;Отступы
Gui, Add, Text, vOSDText cFFFFFF, %Label% %Gain% dB									;Текст, цвет текста
WinSet, Transparent, 191															;Прозрачность
return

OSD() {																				;функция OSD
GuiControl,, OSDText, %Label% %Gain% dB												;Обновление значений
Gui, Show, x100 y100 NoActivate														;Показать
SetTimer, HideOSD, 2000																;Таймер скрытия
return
HideOSD:																			;ХЗ че, но нужно для таймера
Gui, Hide																			;Скрыть 
return
}

;регулировка усиления--------------------------------------------------------------------------------------------------------------------------------------------------------
WriteGain()																			;Применить значение усиления.
SoundGet, SystemVolume																;Найти текущую системную громкость.
if (SystemVolume != 100) {															;Если системная громкость не максимальна,
SoundSet, 100																		;Установить системную громкость на максимум.
}

$Volume_Up::																		;При нажатии кнопки увеличения громкости,
	if (Gain < GainMax) {															;Если усиление меньше максимального значения,
		Gain+=5																		;Увеличить усиление на (++ - 1дБ; +=5 - 5дБ),
		WriteGain()																	;И записать в конфигурацию с помощью функции WriteGain,
	}
	OSD()																			;Использовать функцию OSD для отображения значения усиления.
return																				;Конец горячей клавиши.

$Volume_Down::																		;При нажатии кнопки уменьшения громкости,
	if (Gain > GainMin) {															;Если усиление больше минимального значения,
		Gain-=5																		;Уменьшить усиление на (++ - 1дБ; +=5 - 5дБ),
		WriteGain()																	;И записать в конфигурацию с помощью функции WriteGain,
	}
	OSD()																			;Использовать функцию OSD для отображения значения усиления.
return																				;Конец горячей клавиши.

WriteGain() {																		;Функция WriteGain отвечает за запись изменений в файл конфигурации.
	FileDelete, %GainFile%.tmp.txt													;Удалить старый файл конфигурации, чтобы подготовить его к перезаписи,
	FileAppend, Preamp: %Gain% dB, %GainFile%.tmp.txt								;Сначала записать во временный файл,
	FileCopy, %GainFile%.tmp.txt, %GainFile%, 1										;Затем перезаписать файл усиления временным файлом,
}																					;Для более плавного процесса регулировки громкости и уменьшения хлопков.

;смена конфига--------------------------------------------------------------------------------------------------------------------------------------------------------
$Volume_Mute::																		;При нажатии кнопки мьют,
	if (Profile == ProfileA) {														;Если текущий профиль - профиль А,
	Profile := ProfileB																;То заменить его на профиль B.
	Label := "Zero:2"
	}
	else {																			;Если текущий профиль - профиль B,
	Profile := ProfileA																;То заменить его на профиль A.
	Label := "NOEMA"
	}
	WriteProfile()																	;Записать в конфигурацию с помощью функции WriteProfile,
	OSD()																			;Использовать функцию OSD для отображения текущего профиля.
	return																			;Конец горячей клавиши.

WriteProfile() {																	;Функция WriteProfile отвечает за запись изменений в файл конфигурации.
	FileDelete, %ProfileFile%.tmp.txt												;Удалить старый файл конфигурации, чтобы подготовить его к перезаписи,
	FileAppend, Include: %Profile%, %ProfileFile%.tmp.txt							;Сначала записать во временный файл,
	FileCopy, %ProfileFile%.tmp.txt, %ProfileFile%, 1								;Затем перезаписать файл профиля временным файлом,
}																					;Для более плавного процесса регулировки громкости и уменьшения хлопков.
